---
title: 'Quantifiers: Part 3'
date: 2025-11-25
permalink: /posts/2025/11/blog-post-8/
tags:
  - lean
  - logic
  - quantifiers
  - forall
  - exists
  - negation
---

Now that we are acquainted with the existential and universal quantifiers, we'll look at how negation interacts with each of them. 

## DeMorgans Laws for quantifiers

To say that something holds "For all elements" implies that the property holds for each element. In other words, if x1, x2, x3, ... are elements and the property P holds for each: P(x1) ∧ P(x2) ∧ P(x3) ∧ ... must hold. By DeMorgans law, we can distribute a negation across conjunctions to get disjunctions: ¬P(x1) ∨ ¬P(x2) ∨ ¬P(x3) ∨ ... etc.


Intuitively, negating a universal quantifier satisifies:
`¬(∀x P(x)) ≡ ∃x ¬P(x)`


Applying similar reasoning, to say that some property holds for "at least one element" implies that P(x1) ∨ P(x2) ∨ P(x3) ... must be true. Again, by DeMorgans Laws, we can distribute the negation across disjunctions to get conjunctions: ¬P(x1) ∧ ¬P(x2) ∧ ¬P(x3) ∧ ... etc.

Following this analogy, negating an existential satisfies:
`¬(∃x P(x)) ≡ ∀x ¬P(x)`

## Negation of Universal quantifier

```lean 
example (h : ∀ x, P x → Q x) : (∃ x, ¬Q x) → ∃ x, ¬P x := by
  intro a           -- assume ∃ x, ¬Q x and show ∃ x, ¬P x
  cases a with
  | intro b hb =>   -- let b s.t. ¬Q b
  exists b          -- suppose (bwoc)
  intro npb         -- P b
  apply hb          -- show Q b
  exact h b npb     -- derive Qb from h using witness b and P b

example (h : ∃ x, P x) : ¬∀ x, ¬P x := by
  obtain ⟨ a, ha ⟩ := h -- let a s.t. P a
  push_neg -- this is a shortcut since it uses what we're trying to prove
  exists a -- use exists tactic with witness a

example (h : ∃ x, P x) : ¬(∀ x, ¬P x) := by
  intro x_forall -- bwoc, suppose ∀ x, ¬P x
  cases h with   -- Let a s.t. P a by hypothesis h
  | intro a ha =>
  have not_ha := x_forall a -- then ¬ P a
  contradiction

```
In words: the negation of "for all x, P(x)" is "there exists an x for which P(x) is false."  
Example: "Not all swans are white" ≡ "There exists a swan that is not white."  
Lesson: To refute a universal claim produce a single counterexample. When you move a negation past ∀, flip it to ∃ and push the negation inside.

```lean

example : ¬(∀x, P x) ↔ ∃ x, ¬P x := by 
  constructor
  · -- ⊢ ∀x, P x → ∃ x, ¬ P x
    intro h
    -- h: ¬∀ x, P x
    -- ⊢ ∃ x, ¬P x
    push_neg at h
    -- h : ∃ x, ¬P x
    exact h
  · -- ⊢ ∃x, ¬P x → ¬(∀ x, P x)
    intro h
    -- h : ∃ x, ¬P x
    -- ⊢ ¬ (∀x, P x)
    obtain ⟨ n, hn ⟩ := h
    -- n : ℕ 
    -- hn : ¬P n
    push_neg
    -- ∃ x, ¬P x
    use n
```

## Negation of Existential quantifier

¬(∃x P(x)) ≡ ∀x ¬P(x)

In words: the negation of "there exists an x with P(x)" is "for every x, P(x) is false."  
Example: "There is no even prime greater than 2" ≡ "For every even number, it is not prime (or no even number > 2 is prime)."  
Lesson: To prove a negated existential, show a property holds for all elements (often via a general argument). Note: on an empty domain ∀-statements are vacuously true, so ¬∃ is true there.

```lean
example : ¬(∃x, P x) ↔ ∀x, ¬P x := by
  constructor
  · -- ⊢ ¬∃ x, P x → ∀ x, ¬P x
    intro h
    -- h : ¬∃ x, P x
    -- ⊢ ∀ x, ¬P x
    intro n
    -- n : ℕ
    -- ⊢ ¬P n
    intro hn
    -- hn : P n
    -- ⊢ False
    apply h
    -- ⊢ ∃ x, P x
    use n
    
  · -- ⊢ ∀ x, ¬P → ¬∃ x, P x
    intro h
    -- h : ∀ x, ¬P x
    -- ⊢ ¬∃ x, P x
    intro hp
    -- hp : ∃ x, P x
    -- ⊢ False
    obtain ⟨ a, pa ⟩ := hp
    -- a : ℕ
    -- ha : P a
    -- ⊢ False
    have npa := h a
    -- npa : ¬P a
    -- ⊢ False
    exact npa pa

```

## De Morgan's Laws with quantifiers

Negation distributes over logical connectives inside quantifiers using De Morgan flips:

- ¬(∀x (P(x) ∧ Q(x))) ≡ ∃x (¬P(x) ∨ ¬Q(x))  
- ¬(∃x (P(x) ∨ Q(x))) ≡ ∀x (¬P(x) ∧ ¬Q(x))

Example: Negating "Every student passed both exams" yields "There is a student who failed at least one exam."  
Lesson: Systematically move negation inward by (1) flipping ∀↔∃, and (2) applying De Morgan to logical connectives. This is essential for forming precise counterexamples, constructing contrapositives, and performing proof by contradiction.